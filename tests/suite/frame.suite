# -*- tcl -*- 
## (c) 2019 Andreas Kupries
# # ## ### ##### ######## ############# #####################
## Generic testsuite to check the behaviour of frame objects.

# fkind - frame type
# fdata - input/output data/references
dict with fdata {}
# - _hq   - has? result
# - _it   - iterable? result
# - _nl   - nil? result
# - _vc   - cons value, post processor for raw value
# - _fd   - field query result, and additional test options.
# - _id   - iter result, and additional test options.
# - _vi   - visit result

proc VAL  {} { global fkind _vc ; {*}$_vc [v frame/$fkind] }
proc VALs {} { global fkind     ;          v frame/$fkind.to_s }

# [ok] constructor V
# [ok] field K
# [ok] has? K
# [ok] iter C S
# [ok] iterable?
# [ok] nil?
# [ok] value
# [..] visit

# [..] TODO destructor (confirm that owned objects (children) are destroyed as well)

# # ## ### ##### ######## ############# #####################
## Constructor

if {$fkind ne "null"} {
    tcltest::test frame-${fkind}-cons-1.0 "cons, wrong args, not enough" -body {
	mustache frame $fkind new
    } -returnCodes error \
	-result [M' {wrong # args: should be "mustache frame @ new val"} @ $fkind]

    set r [M' {wrong # args: should be "mustache frame @ new val"} @ $fkind]
} else {
    set r [M' {wrong # args: should be "mustache frame @ new ?val?"} @ $fkind]
}
   
tcltest::test frame-${fkind}-cons-1.1 "cons, wrong args, too many" -body {
    mustache frame $fkind new [VAL] X
} -returnCodes error \
    -result $r

tcltest::test frame-${fkind}-cons-2.0 "cons" -body {
    set o [mustache frame $fkind new [VAL]]
} -cleanup {
    $o destroy
    unset -nocomplain o
} -result {::oo::Obj*} -match glob

# # ## ### ##### ######## ############# #####################
## has?

tcltest::test frame-${fkind}-has?-1.0 "has?, wrong args, not enough" -setup {
    mustache frame $fkind create FR [VAL]
} -cleanup {
    FR destroy
} -body {
    FR has?
} -returnCodes error -result {wrong # args: should be "FR has? k"}

tcltest::test frame-${fkind}-has?-1.1 "has?, wrong args, too many" -setup {
    mustache frame $fkind create FR [VAL]
} -cleanup {
    FR destroy
} -body {
    FR has? FIELD X
} -returnCodes error -result {wrong # args: should be "FR has? k"}

tcltest::test frame-${fkind}-has?-2.0 "has?, result" -setup {
    mustache frame $fkind create FR [VAL]
} -cleanup {
    FR destroy
} -body {
    FR has? FIELD
} -result $_hq

# # ## ### ##### ######## ############# #####################
## iterable?

tcltest::test frame-${fkind}-iterable?-1.0 "iterable?, wrong args, too many" -setup {
    mustache frame $fkind create FR [VAL]
} -cleanup {
    FR destroy
} -body {
    FR iterable? X
} -returnCodes error -result {wrong # args: should be "FR iterable?"}

tcltest::test frame-${fkind}-iterable?-2.0 "iterable?, result" -setup {
    mustache frame $fkind create FR [VAL]
} -cleanup {
    FR destroy
} -body {
    FR iterable?
} -result $_it

# # ## ### ##### ######## ############# #####################
## nil?

tcltest::test frame-${fkind}-nil?-1.0 "nil?, wrong args, too many" -setup {
    mustache frame $fkind create FR [VAL]
} -cleanup {
    FR destroy
} -body {
    FR nil? X
} -returnCodes error -result {wrong # args: should be "FR nil?"}

tcltest::test frame-${fkind}-nil?-2.0 "nil?, result" -setup {
    mustache frame $fkind create FR [VAL]
} -cleanup {
    FR destroy
} -body {
    FR nil?
} -result $_nl

# # ## ### ##### ######## ############# #####################
## value

tcltest::test frame-${fkind}-value-1.0 "value, wrong args, too many" -setup {
    mustache frame $fkind create FR [VAL]
} -cleanup {
    FR destroy
} -body {
    FR value X
} -returnCodes error -result {wrong # args: should be "FR value"}

tcltest::test frame-${fkind}-value-2.0 "value, result" -setup {
    mustache frame $fkind create FR [VAL]
} -cleanup {
    FR destroy
} -body {
    FR value
} -result [VALs]

# # ## ### ##### ######## ############# #####################
## field

tcltest::test frame-${fkind}-field-1.0 "field, wrong args, not enough" -setup {
    mustache frame $fkind create FR [VAL]
} -cleanup {
    FR destroy
} -body {
    FR field
} -returnCodes error -result {wrong # args: should be "FR field k"}

tcltest::test frame-${fkind}-field-1.1 "field, wrong args, too many" -setup {
    mustache frame $fkind create FR [VAL]
} -cleanup {
    FR destroy
} -body {
    FR field FIELD X
} -returnCodes error -result {wrong # args: should be "FR field k"}

tcltest::test frame-${fkind}-field-2.0 "field, result" -setup {
    mustache frame $fkind create FR [VAL]
} -cleanup {
    FR destroy
} -body {
    FR field FIELD
} -result {*}$_fd

# # ## ### ##### ######## ############# #####################
## iter

tcltest::test frame-${fkind}-iter-1.0 "iter, wrong args, not enough" -setup {
    mustache frame $fkind create FR [VAL]
} -cleanup {
    FR destroy
} -body {
    FR iter
} -returnCodes error -result {wrong # args: should be "FR iter context script"}

tcltest::test frame-${fkind}-iter-1.1 "iter, wrong args, not enough" -setup {
    mustache frame $fkind create FR [VAL]
} -cleanup {
    FR destroy
} -body {
    FR iter C
} -returnCodes error -result {wrong # args: should be "FR iter context script"}

tcltest::test frame-${fkind}-iter-1.2 "iter, wrong args, too many" -setup {
    mustache frame $fkind create FR [VAL]
} -cleanup {
    FR destroy
} -body {
    FR iter C S X
} -returnCodes error -result {wrong # args: should be "FR iter context script"}

tcltest::test frame-${fkind}-iter-2.0 "iter, result" -setup {
    mustache frame $fkind create FR [VAL]
    set _trace {}
    proc C {args} { lappend ::_trace $args }
} -cleanup {
    FR destroy
    unset _trace
    rename C {}
} -body {
    FR iter C { C dot }
    set _trace
} -result {*}$_id

# # ## ### ##### ######## ############# #####################
## visit

tcltest::test frame-${fkind}-visit-1.0 "visit" -setup {
    mustache frame $fkind create FR [VAL]
    set r {}
} -cleanup {
    FR destroy
    unset r
} -body {
    FR visit lappend r
    join $r \n
} -match glob -result [join $_vi \n]

# # ## ### ##### ######## ############# #####################
unset -nocomplain _hq _it _nl _fd _vc _vi
rename VAL {}
rename VALs {}
return
